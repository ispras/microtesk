op controlinst16 = call16|jumps16|ret_16|loop_16

op jumps16 = j16|jeq16|jgez|jgtz|lt16|jlez|jne16|jltz|jz16|jnz_16|ji_16

op call16(disp:card(8))
   syntax = format("call 0x%08x", __IADDR + coerce(int(32), disp))
   image = format("%8b01011100",disp)
   action = 	{ 
	  	if (FCX == 0)
		then
			;//TRAP(FCU)
		endif;
		cdc_incr(TMP_1);
		if (CDEFLAG && TMP_1)
		then
			;//TRAP(CDO)
		endif;
		CDEFLAG = 1;
		TMP_1 = PC+2;
		TMP_2 = FCX;
		TMP_3 = FCXS::0b000000::FCXO::0b000000;
		loadWord(TMP_4,TMP_3);
		storeWord(PCXI,TMP_3);
		storeWord(PSW,TMP_3+4);
		storeWord(A[10],TMP_3+8);
		storeWord(A[11],TMP_3+12);
		storeWord(D[8],TMP_3+16);
		storeWord(D[9],TMP_3+20);
		storeWord(D[10],TMP_3+24);
		storeWord(D[11],TMP_3+28);
		storeWord(A[12],TMP_3+32);
		storeWord(A[13],TMP_3+36);
		storeWord(A[14],TMP_3+40);
		storeWord(A[15],TMP_3+44);
		storeWord(D[12],TMP_3+48);
		storeWord(D[13],TMP_3+52);
		storeWord(D[14],TMP_3+56);
		storeWord(D[15],TMP_3+60);
		PCPN = CCPN;
		PIE = IE;
		UL = 1;
		PCXI<0..19> = FCX<0..19>;
		FCX<0..19> = TMP_4<0..19>;
		PC = PC + coerce(int(32),disp);
		A[11] = TMP_1;
		if (TMP_2 == LCX)
		then 
			;//TRAP(FCD)
		endif;
   		}
   		otawa_sem = "SAVE_CONTEXT; SETI(A11, IADDR + 2); SETI(T1, IADDR + 2 * S8(0)); BRANCH(T1);"

op j16 (const8:card(8), const:card(32))
   predecode = 	{ 
		const<0..7> = const8;
		if (const8<7..7>==1)then
		    const<8..31>=0xFFFFFF;
		endif;
		const = 2*const;
   		}
   syntax = format("j 0x%08x", __IADDR + const)
   image = format("%8b00111100%0b",const8,const)
   action = 	{ 
	  	PC = PC + const;
   		}
   	otawa_sem = "SETI(T1, IADDR + S8(0) * 2); BRANCH(T1);"

op jeq16 = jeq16_1|jeq16_2

op jeq16_1 (b:reg_d, disp4:card(4), disp:card(32))
   predecode = { 
		disp = 0;
		disp<0..3> = disp4;
		disp = 2*disp;
   }
   syntax = format("jeq d15, %s, 0x%08x", b.syntax, __IADDR + disp)
   image = format("%s%4b00111110%0b",b.image,disp4,disp)
   action = 	{
		if(D[15]==b)
		then 
			PC = PC + disp; 
		endif;
		}
	otawa_sem = "CMP(T1, D15, D(0)); IF(EQ, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op jeq16_2 (const4:card(4), disp4:card(4), disp:card(32),const:card(32))
    predecode = { 
		disp = 0;
		disp<0..3> = disp4<0..3>;
		disp = 2*disp; 
		const<0..3> = const4;
	        if (const4<3..3>==1)then
		   const<4..31>=0xFFFFFFF;
		endif;}
    syntax = format("jeq d15, %d, 0x%08x", const4, __IADDR + disp)
    image = format("%4b%4b00011110%0b%0b",const4,disp4,disp,const)
    action = 	{
		if(D[15]==const)
		then 
			PC = PC + disp; 
		endif;
		}
		otawa_sem = "SETI(T1, S4(0)); CMP(T1, D15, T1); IF(EQ, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op jgez (b:reg_d, disp4:card(4), disp:card(32))
   predecode = 	{
		disp = 0; 
		disp<0..3> = disp4<0..3>;
		disp = disp*2;
  		}
    syntax = format("jgez %s, 0x%08x", b.syntax, __IADDR + disp)
    image = format("%s%4b11001110%0b",b.image,disp4,disp)
    action = 	{
		if(b >= 0)
		then 
			PC = PC + disp; 
		endif;
		}
	otawa_sem = "SETI(T1, 0), CMP(T1, D(0), T1); IF(GE, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op jgtz (b:reg_d,disp4:card(4),disp:card(32))
   predecode = 	{
		disp = 0; 
		disp<0..3> = disp4<0..3>;
		disp = disp*2;
  		}
    syntax = format("jgtz %s, 0x%08x", b.syntax, __IADDR + disp)
    image = format("%s%4b01001110%0b",b.image,disp4,disp)
    action = 	{
		if(b > 0)
		then 
			PC = PC + disp; 
		endif;
		}
	otawa_sem = "SETI(T1, 0), CMP(T1, D(0), T1); IF(GT, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op jlez (b:reg_d,disp4:card(4),disp:card(32))
   predecode = 	{
		disp = 0; 
		disp<0..3> = disp4<0..3>;
		disp = disp*2;
  		}
    syntax = format("jlez %s, 0x%08x", b.syntax, __IADDR + disp)
    image = format("%s%4b10001110%0b",b.image,disp4,disp)
    action = 	{
		if(b <= 0)
		then 
			PC = PC + disp; 
		endif;
		}
	otawa_sem = "SETI(T1, 0), CMP(T1, D(0), T1); IF(LE, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op jltz (b:reg_d, disp4:card(4), disp:card(32))
    predecode = {
		disp = 0;
		disp<0..3> = disp4<0..3>;
		disp = disp*2;
    		}
    syntax = format("jltz %s, 0x%08x", b.syntax, __IADDR + disp)
    image = format("%s%4b00001110%0b",b.image,disp4,disp)
    action = 	{
		if(b<0)
		then
			PC = PC + disp;
		endif;
		}
	otawa_sem = "SETI(T1, 0), CMP(T1, D(0), T1); IF(LE, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op jne16 = jne16_reg|jne16_const

op jne16_const (const4:card(4), disp4:card(4), disp:card(32), const:card(32))
    predecode = {
		disp = 0; 
		disp<0..3> = disp4<0..3>;
		disp = 2*disp; 
		const = 0;
		const<0..3> = const4<0..3>;
	        if (const4<3..3>==1)then
		   const<4..31>=0xFFFFFFF;
		endif;}
    syntax = format("jne d15, %d, 0x%08x", const4, __IADDR + disp)
    image = format("%4b%4b01011110%0b%0b",const4,disp4,disp,const)
    action = 	{
		if(D[15]!=const)
		then 
			PC = PC + disp; 
		endif;
		}
	otawa_sem = "SETI(T1, S4(0)), CMP(T1, D15, T1); IF(NE, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op jne16_reg (b:reg_d, disp4:card(4), disp:card(32))
   predecode = {
		disp = 0; 
		disp<0..3> = disp4<0..3>;
		disp = 2*disp;
   }
   syntax = format("jeq d15, %s, %d", b.syntax, __IADDR + disp)
   image = format("%s%4b01111110%0b",b.image,disp4,disp)
   action = 	{
		if(D[15]!=b)
		then 
			PC = PC + disp; 
		endif;
		}
	otawa_sem = "CMP(T1, D15, D(0)); IF(NE, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op jz16 = jz_t_16|jz_disp4_16|jz_disp8_16|jz_a_16

op jz_t_16 (n:card(4), disp4:card(4), disp:card(32))
   predecode = { 
		disp = 0;
	     	disp = disp4;
	       	disp = 2*disp;
  }
   syntax = format("jz.t d15, %d, 0x%08x", n, __IADDR + disp)
   image = format("%4b%4b00101110%0b",n,disp4,disp)
   action = 	{ 
		if (D[15]<n..n>)
		then 
			PC = PC + disp; 
		endif; 
		}
	otawa_sem = "SCRATCH(T1); IF(NE, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op jz_disp8_16(disp8:card(8), disp:card(32),foo:card(8))
   predecode = {
	     disp = disp8;
	     if (disp8<7..7>==1)then
		disp<8..31>=0xFFFFFF;
	     endif;
	     disp = 2*disp;
   }
   syntax = format ("jz d15, %d", __IADDR + disp)	
   image = format("%8b01101110%0b%0b",disp8,foo,disp)
   action = 	{
	  	if (D[15]==0) 
		then
	     		PC = PC + disp;
	  	endif;
   		}
	otawa_sem = "SETI(T1, 0); CMP(T1, D15, T1); IF(EQ, T1, 2); SETI(T1, IADDR + 2 * S8(0)); BRANCH(T1);"

op jz_disp4_16 (b:reg_d, disp4:card(4), disp:card(32))
   predecode = {
	     disp = disp4;
	     disp = 2*disp;
   }
   syntax = format ("jz %s, 0x%08x", b.syntax, __IADDR + disp)
   image = format("%s%4b01110110%0b",b.image,disp4,disp)
   action = 	{
	  	if (b==0) 
		then
	     		PC = PC + disp;
	  	endif;
   }
	otawa_sem = "SETI(T1, 0); CMP(T1, D(0), T1); IF(EQ, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1); "

op ret_16 (foo:card(4))
   syntax = "ret"
   image = format("1001%4b00000000",foo)
   action = 	{
		cdc_decr(TMP_1);
		if (CDEFLAG && TMP_1)
		then
			;//TRAP(CDU)
		endif;
		if(PCXI<0..19> == 0)
		then
			;//TRAP(CSU)
		endif;
		if (UL == 0)
		then
			;//TRAP(CTYP)
		endif;
		PC = A[11]<1..31>::0b0;
		TMP_3 = PCXS::0b000000::PCXO::0b000000;
		loadWord(TMP_1,TMP_3);
		loadWord(TMP_2,TMP_3+4);
		loadWord(A[10],TMP_3+8);
		loadWord(A[11],TMP_3+12);
		loadWord(D[8],TMP_3+16);
		loadWord(D[9],TMP_3+20);
		loadWord(D[10],TMP_3+24);
		loadWord(D[11],TMP_3+28);
		loadWord(A[12],TMP_3+32);
		loadWord(A[13],TMP_3+36);
		loadWord(A[14],TMP_3+40);
		loadWord(A[15],TMP_3+44);
		loadWord(D[12],TMP_3+48);
		loadWord(D[13],TMP_3+52);
		loadWord(D[14],TMP_3+56);
		loadWord(D[15],TMP_3+60);
		
		loadWord(TMP_1,TMP_3);
		loadWord(PSW,TMP_3+4);
		loadWord(A[10],TMP_3+8);
		loadWord(A[11],TMP_3+12);
		loadWord(D[8],TMP_3+16);
		loadWord(D[9],TMP_3+20);
		loadWord(D[10],TMP_3+24);
		loadWord(D[11],TMP_3+28);
		loadWord(A[12],TMP_3+32);
		loadWord(A[13],TMP_3+36);
		loadWord(A[14],TMP_3+40);
		loadWord(A[15],TMP_3+44);
		loadWord(D[12],TMP_3+48);
		loadWord(D[13],TMP_3+52);
		loadWord(D[14],TMP_3+56);
		loadWord(D[15],TMP_3+60);

		storeWord(FCX,TMP_3);
		FCX<0..19> = PCXI<0..19>;
		PCXI = TMP_1;
		PSW = TMP_1<26..31>::PSW<24..25>::TMP_1<0..23>;
		}
	otawa_sem = "BRANCH(A11); LOAD_CONTEXT;"

op loop_16(b:reg_a, disp4:card(4), disp:card(32))
   predecode =	{
	     	disp = 0xffffffe0;
	     	disp<1..4> = disp4<0..3>;
   		}
   syntax = format("loop %s, %d", b.syntax, __IADDR + disp)
   image = format("%s%4b11111100%0b",b.image,disp4,disp)
   action = 	{
		if(b!=0)
		then
			PC = PC + disp;
		endif;
		b = b-1;
   		}
   	otawa_sem = "SETI(T1, 0); CMP(T1, A(0), T1); IF(NE, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1); SETI(T1, 0); SUB(D(0), D(0), T1); "

op jnz_16 = jnz_16_1|jnz_16_2|jnz_a_16|jnz_t_16

op jnz_16_1(disp8:card(8), disp:card(32), foo:card(8))
   predecode = {
	     disp = disp8;
	     if (disp8<7..7>==1)then
		disp<8..31>=0xFFFFFF;
	     endif;
	     disp = 2*disp;
   }
   syntax = format ("jnz d15, 0x%08x", __IADDR + disp)
   image = format("%8b11101110%0b%0b",disp8,foo,disp)
   action = 	{
	  	if (D[15]!=0) 
		then
	     		PC = PC + disp;
	  	endif;
   }
   otawa_sem = "SETI(T1, 0), CMP(T1, D15, T1); IF(NE, T1, 2); SETI(T1, IADDR + 2 * S8(0)); BRANCH(T1);"

op jnz_16_2 (b:reg_d, disp4:card(4), disp:card(32))
   predecode = 	{
		disp = 0;
	     	disp = disp4;
	     	disp = 2*disp;
   		}
   syntax = format ("jnz %s, %d", b.syntax, __IADDR + disp)
   image = format("%s%4b11110110%0b",b.image,disp4,disp)
   action = 	{
	  	if (b!=0) 
		then
	     		PC = PC + disp;
	  	endif;
   		}
   	otawa_sem = "SETI(T1, 0); CMP(T1, D(0), T1); IF(NE, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op ji_16 (a:reg_a, addr:card(32), foo:card(8))
   syntax = format("ji %s", a.syntax)
   image = format("0000%s11011100%0b%0b",a.image,foo,addr)
   action = 	{
		addr = a;
	     	addr<0..0>=0b0;
	  	PC = addr;
   		}
   	otawa_sem = "BRANCH(A(0));"
   	

op jz_a_16 (b:reg_d, disp4:card(4), disp:card(32))
   predecode = {
	     disp = disp4;
	     disp = 2*disp;
   }
   syntax = format ("jz.a %s, 0x%08x", b.syntax, __IADDR + disp)
   image = format("%s%4b10111100%0b",b.image,disp4,disp)
   action = 	{
	  	if (b==0) 
		then
	     		PC = PC + disp;
	  	endif;
   		}
   	otawa_sem = "SETI(T1, 0); CMP(T1, D(0), T1); IF(EQ, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);" 

op jnz_a_16 (b:reg_d, disp4:card(4), disp:card(32))
   predecode = {
		disp = 0;
	     	disp = disp4;
	     	disp = 2*disp;
   }
   syntax = format ("jnz.a %s, 0x%08x", b.syntax, __IADDR + disp)
   image = format("%s%4b01111100%0b",b.image,disp4,disp)
   action = 	{
	  	if (b!=0) 
		then
	     		PC = PC + disp;
	  	endif;
   	}
   	otawa_sem = "SETI(T1, 0); CMP(T1, A(0), T1); IF(EQ, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);" 


// jnz.t instruction
op jnz_t_16 (disp:card(32),n:card(4),disp4:card(4))
   predecode = 	{
		disp = 0;
	     	disp = disp4;
	     	disp = 2*disp;
   		}
   syntax = format ("jnz.t d15, %d, 0x%08x", n, __IADDR + disp)
   image = format("%4b%4b10101110%0b",n,disp4,disp)
   action = 	{
	  	if (D[15]<n..n>) 
		then
	     		PC = PC + disp;
	  	endif;
   		}
   	otawa_sem = "SCRATCH(T1); IF(EQ, T1, 2); SETI(T1, IADDR + 2 * U4(1)); BRANCH(T1);"

op lt16 = lt16_reg|lt16_const

op lt16_reg (a:reg_d, b:reg_d)
   syntax = format ("lt d15,%s,%s",a.syntax,b.syntax)
   image = format("%s%s01111010",a.image,b.image)
   action = 	{
	  	D[15] = (a<b);
   		}
   	otawa_sem = "SCRATCH(D15);" 

op lt16_const (a:reg_d, const:const4)
   syntax = format ("lt d15,%s,%s",a.syntax,const.syntax)
   image = format("%s%s11111010",const.image,a.image)
   action = 	{
		zero_ext32(TMP_1,const,4);
	  	D[15] = (a<TMP_1);
   		}
   	otawa_sem = "SCRATCH(D15);" 
   
