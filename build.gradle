/*
 * Copyright 2015-2019 ISP RAS (http://www.ispras.ru)
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License
 * is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express
 * or implied. See the License for the specific language governing permissions and limitations under
 * the License.
 */

import net.researchgate.release.ReleasePlugin

import java.text.SimpleDateFormat
import java.util.regex.Matcher

buildscript {
  repositories {
    ivy {
      url = 'https://forge.ispras.ru/repo'
      layout 'pattern', {
        artifact '[organization]/[ext]s/[artifact]-[revision](.[ext])'
      }
    }
    jcenter()
  }

  dependencies {
    classpath 'gradle:gradle-os-plugin:1.0'
  }
}

plugins {
  id 'net.saliman.properties' version '1.4.4'
  id 'net.researchgate.release' version '2.6.0'
  id 'org.sonarqube' version '2.6.2'
}

apply plugin: 'os'
apply plugin: 'java'
apply plugin: 'java-library-distribution'
apply plugin: 'antlr'
apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'

def repoUrlStr = repoUrl
def repoUserStr = hasProperty('repoUser') ? repoUser : "$System.env._REPO_USER"
def repoPasswordStr = hasProperty('repoPassword') ? repoPassword : "$System.env._REPO_PASSWORD"

repositories {
  maven {
    url repoUrlStr + 'releases'

    credentials {
      username repoUserStr
      password repoPasswordStr
    }
    authentication {
      basic(BasicAuthentication)
    }
  }
  maven {
    url repoUrlStr + 'snapshots'

    credentials {
      username repoUserStr
      password repoPasswordStr
    }
    authentication {
      basic(BasicAuthentication)
    }
  }
  ivy {
    url = 'http://forge.ispras.ru/repo'
    layout 'pattern', {
      artifact '[organization]/[ext]s/[artifact]-[revision](.[ext])'
      // z3
      artifact '[organization]/[artifact]-[revision](.[ext])'
    }
  }
  jcenter()
  mavenLocal()
}

uploadArchives {
  repositories {
    mavenLocal()
  }
}

release {
  failOnCommitNeeded = false
  failOnUnversionedFiles = false

  versionPatterns = [
    // Increments build number: "0.2.5-alpha-150428" => "0.2.6-alpha-150428"
    /(^\d+\.\d+\.)(\d+)(-[^-]*)(-[^-]*$)/:
    { Matcher m, Project p -> m.replaceAll("${ m[0][1] }${ (m[0][2] as int) + 1 }${ m[0][3] }" ) }
  ]
}

static String getCurrentDateString()  {
  new SimpleDateFormat( "yyMMdd" ).format( new Date() )
}

unSnapshotVersion.doLast {
  def version = project.version.toString()
  version += '-' + getCurrentDateString()
   project.plugins.getPlugin( net.researchgate.release.ReleasePlugin.class )
  .updateVersionProperty( version )
}

String z3
String z3path

String cvc4
String cvc4path

windows {
  z3 = 'z3:z3:4.3.0-x64:@zip'
  z3path = 'tools/z3/bin/z3.exe'

  cvc4 = 'cvc4:cvc4:1.4-win32-opt:@exe'
  cvc4path = 'tools/cvc4-windows.exe'
}

unix {
  z3 = 'z3:z3:x64-4.1:@tar.gz'
  z3path = 'tools/z3/bin/z3'

  cvc4 = 'cvc4:cvc4:2015-05-12-x86_64-linux-opt:@bin'
  cvc4path = 'tools/cvc4-unix.bin'
}

macOs {
  z3 = 'z3:z3:4.3.2.7c12ab47165a-x64-osx-10.8.2:@zip'
  z3path = 'tools/z3/bin/z3'

  // Currently, no suitable CVC4 for OS X is available. For now Unix binaries
  // are specified here. These temp values are required for the build to complete.
  cvc4 = 'cvc4:cvc4:2015-05-12-x86_64-linux-opt:@bin'
  cvc4path = 'tools/cvc4-unix.bin'
}

configurations {
  jruby
  qemu
  // jython
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {
  testCompile 'junit:junit:4.11'
  testCompile 'hamcrest:hamcrest-core:1.3'
  testRuntime z3
  testRuntime cvc4

  antlr 'org.antlr:antlr:3.5.2'
  compile 'org.glassfish:javax.json:1.1.2'
  compile 'commons-cli:commons-cli:1.2'
  compile 'commons-lang:commons-lang:2.4'
  compile 'org.jruby:jruby-complete:1.7.25'
  jruby 'org.jruby:jruby-complete:1.7.25'
  // compile 'org.python:jython:2.7.0'
  // jython 'org.python:jython:2.7.0'
  compile 'sat4j:org.sat4j.core:v20130525'

  compile ("fortress:fortress:${fortressVersion}") { changing = true }
  compile ("castle:castle:${castleVersion}") { changing = true }
  compile ("testbase:testbase:${testbaseVersion}") { changing = true }
  compile ("jsoftfloat:jsoftfloat:${jsoftfloatVersion}") { changing = true }
  compile ("aspectraceapi:aspectraceapi:${aspectraceapiVersion}") { changing = true }

  qemu("qemu4v:qemu4v:${qemuVersion}:@tar.gz") { changing = true }

  // TODO: unused Aspectrace dependencies
  // compile 'org.codehaus.woodstox:wstx-asl:3.2.4'
  // compile ('coverage:coverage:+') { changing = true }
  // compile ('aspectrace:aspectrace:+') { changing = true }

  checkstyle 'com.puppycrawl.tools:checkstyle:6.12.1'
  pmd 'net.sourceforge.pmd:pmd-core:5.4.0'
  pmd 'net.sourceforge.pmd:pmd-java:5.4.0'
}

configurations {
  noEclipse
}

dependencies {
  noEclipse z3
  noEclipse cvc4
}

eclipse {
  classpath {
    downloadJavadoc=false
    downloadSources=true

    minusConfigurations += [ configurations.noEclipse ]
    sourceSets -= [ sourceSets.main, sourceSets.test ]

    file {
      withXml {
        def node = it.asNode()
        node.appendNode('classpathentry', [kind: 'src', path: 'src/main/java/core'])
        node.appendNode('classpathentry', [kind: 'src', path: 'src/main/resources/core'])
        node.appendNode('classpathentry', [kind: 'src', path: 'src/test/java/core'])

        node.appendNode('classpathentry', [kind: 'src', path: 'src/main/java/plugins/mmu'])
        node.appendNode('classpathentry', [kind: 'src', path: 'src/main/resources/plugins/mmu'])
        node.appendNode('classpathentry', [kind: 'src', path: 'src/test/java/plugins/mmu'])

        node.appendNode('classpathentry', [kind: 'src', path: 'build/generated-src/antlr/main'])
      }
    }
  }
}

task getZ3(type: Copy) {
  windows {
    from zipTree( configurations.testRuntime.fileCollection { dep -> dep.name == 'z3' }.singleFile )
  }
  unix  {
    from tarTree( configurations.testRuntime.fileCollection { dep -> dep.name == 'z3' }.singleFile )
  }
  macOs {
    from zipTree( configurations.testRuntime.fileCollection { dep -> dep.name == 'z3' }.singleFile )
  }
  into 'tools/z3'
  eachFile {
    it.relativePath = RelativePath.parse( true, it.relativePath.segments[1..-1].join('/') )
  }
}

test.dependsOn getZ3

task getCVC4(type: Copy) {
  windows {
    from configurations.testRuntime.fileCollection { dep -> dep.name == 'cvc4' }.singleFile
    rename '.*', 'cvc4-windows.exe'
  }
  unix  {
    from configurations.testRuntime.fileCollection { dep -> dep.name == 'cvc4' }.singleFile
    rename '.*', 'cvc4-unix.bin'
    fileMode = 0755
  }
  into 'tools'
}

test.dependsOn getCVC4

task copyConfig(type: Copy) {
  from "${project.projectDir}/src/main/resources/core/config.xml"
  into "${project.projectDir}/build/resources/test"
}

test.dependsOn copyConfig

task copyQemu(type: Copy) {
  from tarTree(configurations.qemu.fileCollection { dep -> dep.name == 'qemu4v' }.singleFile)
  into "${project.projectDir}/build/tools/qemu"
}

test.dependsOn copyQemu

task copyStg(type: Copy) {
  from "${project.projectDir}/src/main/resources/core/stg"
  into "${project.projectDir}/build/resources/test/stg"

  from "${project.projectDir}/src/main/resources/plugins/mmu/stg"
  into "${project.projectDir}/build/resources/test/stg"
}

test.dependsOn copyStg

task cleanDistribution(type: Delete) {
  delete "${project.projectDir}/build/target"
}

task copyDistribution(type: Copy) {
  includeEmptyDirs = false
  from tarTree("${project.projectDir}/build/distributions/microtesk-${project.version}.tar.gz")
  into "${project.projectDir}/build/target"
  eachFile {FileCopyDetails details ->
    details.path = (details.path - "microtesk-${project.version}")
  }
}

copyDistribution.dependsOn cleanDistribution
copyDistribution.dependsOn distTar
assemble.dependsOn copyDistribution
test.dependsOn copyDistribution

test {
  filter {
    //include all test cases
    includeTestsMatching "*TestCase"
  }

  environment 'Z3_PATH', z3path
  environment 'CVC4_PATH', cvc4path
  environment 'MICROTESK_HOME', "${project.projectDir}/build/target"
  environment 'TEST_PATH', "${project.projectDir}/build/test"
  environment 'QEMU4V_PATH', "${project.projectDir}/build/tools/qemu/bin"
  testLogging.showStandardStreams = true
  ignoreFailures = true
  forkEvery = 1
}

sourceSets {
  main {
    java {
      srcDirs = ['src/main/java/core','src/main/java/plugins/mmu', 'build/generated-src/antlr/main']
    }
  }
  test {
    java {
      srcDirs = ['src/test/java/core','src/test/java/plugins/mmu']
    }
    resources {
      srcDir "${project.projectDir}/build/classes/main"
    }
  }
}

//copying all dependencies attached to 'compile' into a specific folder
task getDependencies(type: Copy) {
  //referring to the 'compile' configuration
  from configurations.compile, configurations.testCompile
  into 'libs'
}

compileJava {
  options.encoding = 'UTF-8'
  sourceCompatibility = '1.7'
  targetCompatibility = '1.7'
}

generateGrammarSource {
  arguments += ["-lib", "src/main/antlrlib/ru/ispras/microtesk/translator/nml/grammar"]
}

checkstyle {
  toolVersion = 6.5
  ignoreFailures = true
  showViolations = false
  configFile=file("${project.projectDir}/config/checkstyle/google_checks.xml")
}

// Needed to exclude files generated by ANTLR from consideration.
checkstyleMain.source = "${project.projectDir}/src/main/java"
checkstyleTest.source = "${project.projectDir}/src/test/java"

pmd {
  ignoreFailures = true
  ruleSetFiles = files("config/pmd/java/basic.xml", "config/pmd/java/braces.xml",
                       "config/pmd/java/clone.xml", "config/pmd/java/codesize.xml",
                       "config/pmd/java/comments.xml", "config/pmd/java/design.xml",
                       "config/pmd/java/empty.xml", "config/pmd/java/finalizers.xml",
                       "config/pmd/java/imports.xml", "config/pmd/java/junit.xml",
                       "config/pmd/java/logging-java.xml", "config/pmd/java/migrating.xml",
                       "config/pmd/java/migrating_to_13.xml", "config/pmd/java/migrating_to_14.xml",
                       "config/pmd/java/migrating_to_15.xml", "config/pmd/java/naming.xml",
                       "config/pmd/java/migrating_to_junit4.xml", "config/pmd/java/strings.xml",
                       "config/pmd/java/optimizations.xml", "config/pmd/java/strictexception.xml",
                       "config/pmd/java/sunsecure.xml", "config/pmd/java/typeresolution.xml",
                       "config/pmd/java/unnecessary.xml", "config/pmd/java/unusedcode.xml")
}

jacoco {
  toolVersion = "0.8.4"
}

jacocoTestReport {
  reports {
    xml.enabled true
    csv.enabled false
  }
}

sonarqube {
  properties {
    property "sonar.host.url", "http://forge.ispras.ru:9000"
    property "sonar.jdbc.url", "jdbc:mysql://localhost:3306/sonar"
    property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
    property "sonar.jdbc.username", "sonar"
    property "sonar.jdbc.password", "sonar"
  }
}

jar {
  archiveName = projectName + '.jar'

  from('LICENSE') {
    into('META-INF')
  }

  exclude('*core*')
  exclude('*plugins*')

  from('src/main/resources/core/config.xml')
  from('src/main/resources/core/stg') {
    into('stg')
  }
  from('src/main/resources/plugins/mmu/stg') {
    into('stg')
  }

  manifest {
    attributes 'Title'  : projectTitle
    attributes 'Version': project.version
    attributes 'Vendor' : 'ISP RAS (http://www.ispras.ru)'
    attributes 'Built-By': System.getProperty('user.name')
    attributes 'Built-JDK': System.getProperty('java.version')
    attributes 'Main-Class': mainClassName
    attributes 'Class-Path': configurations.compile.collect { it.getName() }.join(' ')
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  archiveName = projectName + '-src.jar'
  from(projectDir) {
    into(projectName)
    include('src/')
    include('config/')
    include('gradle/')
  }

  from(files { projectDir.listFiles() }.filter { File file -> file.isFile() ? file : null }) {
    into(projectName)
    exclude('gradle-local.properties')
  }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  archiveName = projectName + '-javadoc.jar'
  from javadoc.destinationDir
}

task copyEtc(type: Copy) {
  from "${project.projectDir}/src/main/etc"
  into "${project.projectDir}/build/target/etc"
}

copyEtc.dependsOn jar

/**/ // EXCLUDED FROM RELEASE (NOT READY)
task copyWhy3(type: Copy) {
  from "${project.projectDir}/src/main/why3"
  into "${project.projectDir}/build/target/lib/why3"
}

copyEtc.dependsOn copyWhy3
//*/

task copyPython(type: Copy) {
  from "${project.projectDir}/src/main/python"
  into "${project.projectDir}/build/target/lib/python"
}

task copyRuby(type: Copy) {
  from "${project.projectDir}/src/main/ruby"
  into "${project.projectDir}/build/target/lib/ruby"
}

task copyTemplates(type: Copy) {
  from "${project.projectDir}/src/main/arch"
  into "${project.projectDir}/build/target/arch"
}

copyTemplates.dependsOn copyPython
copyTemplates.dependsOn copyRuby

task translateCpu(type: JavaExec) {
  classpath = files('build/libs/microtesk.jar')
  classpath += sourceSets.main.runtimeClasspath
  environment 'MICROTESK_HOME', "${project.projectDir}/build/target"
  main = mainClassName
  args = [
    '-t',
    'src/main/arch/demo/cpu/model/cpu.nml',
    '-od',
    'build/generated-src/models'
  ]
}

translateCpu.dependsOn copyEtc

task translateVliw(type: JavaExec) {
  classpath = files('build/libs/microtesk.jar')
  classpath += sourceSets.main.runtimeClasspath
  environment 'MICROTESK_HOME', "${project.projectDir}/build/target"
  main = mainClassName
  args = [
    '-t',
    'src/main/arch/demo/vliw/model/vliw.nml',
    '-od',
    'build/generated-src/models'
  ]
}

translateVliw.dependsOn copyEtc

task translateVmem(type: JavaExec) {
  classpath = files('build/libs/microtesk.jar')
  classpath += sourceSets.main.runtimeClasspath
  environment 'MICROTESK_HOME', "${project.projectDir}/build/target"
  main = mainClassName
  args = [
    '-t',
    'src/main/arch/demo/vmem/model/vmem.nml',
    'src/main/arch/demo/vmem/model/vmem.mmu',
    '-od',
    'build/generated-src/models'
  ]
}

translateVmem.dependsOn copyEtc

task translateMinimips(type: JavaExec) {
  classpath = files('build/libs/microtesk.jar')
  classpath += sourceSets.main.runtimeClasspath
  environment 'MICROTESK_HOME', "${project.projectDir}/build/target"
  main = mainClassName
  args = [
    '-t',
    'src/main/arch/demo/minimips/model/minimips.nml',
    'src/main/arch/demo/minimips/model/mmu/minimips.mmu',
    '-od',
    'build/generated-src/models',
    '-ed',
    'src/main/arch/demo/minimips/extensions'
  ]
}

translateMinimips.dependsOn copyEtc

task translateX86Gnu(type: JavaExec) {
  classpath = files('build/libs/microtesk.jar')
  classpath += sourceSets.main.runtimeClasspath
  environment 'MICROTESK_HOME', "${project.projectDir}/build/target"
  main = mainClassName
  args = [
    '-t',
    'src/main/arch/demo/x86/model/x86.nml',
    '-model-name',
    'x86gnu',
    '-ri',
    'I80386_GNU',
    '-od',
    'build/generated-src/models'
  ]
}

translateX86Gnu.dependsOn copyEtc

task translateX86Nasm(type: JavaExec) {
  classpath = files('build/libs/microtesk.jar')
  classpath += sourceSets.main.runtimeClasspath
  environment 'MICROTESK_HOME', "${project.projectDir}/build/target"
  main = mainClassName
  args = [
    '-t',
    'src/main/arch/demo/x86/model/x86.nml',
    '-model-name',
    'x86nasm',
    '-ri',
    'I80386',
    '-od',
    'build/generated-src/models'
  ]
}

translateX86Nasm.dependsOn copyEtc

task compileModels(type: JavaCompile) {
  source = fileTree(dir: 'build/generated-src/models/src', include: '**/*.java')
  destinationDir = file('build/classes/models')
  classpath = files('build/libs/microtesk.jar')
  classpath += sourceSets.main.runtimeClasspath
}

task jarModels(type: Jar) {
  archiveName = 'models.jar'
  from('build/classes/models')

  manifest {
    attributes 'Title'  : projectTitle + ' Models'
    attributes 'Version': project.version
    attributes 'Vendor' : 'ISP RAS (http://www.ispras.ru)'
    attributes 'Built-By': System.getProperty('user.name')
    attributes 'Built-JDK': System.getProperty('java.version')
    attributes 'Class-Path': projectName + '.jar'
  }
}

task cleanFilesForRDoc(type: Delete) {
  delete "${project.projectDir}/build/tmp/rdoc"
}

task copyFilesForRDoc(type: Copy) {
  from "${project.projectDir}/src/main/ruby"
  into "${project.projectDir}/build/tmp/rdoc"
}

task runRDoc(type: JavaExec) {
  classpath = configurations.jruby
  main = 'org.jruby.Main'
  workingDir = "${project.projectDir}/build/tmp/rdoc"
  args = [
    "${project.projectDir}/build/tmp/rdoc/rdoc.rb",
    "${project.projectDir}/build/tmp/rdoc"
  ]
}

task rdocJar(type: Jar) {
  archiveName = projectName + '-rubydoc.jar'
  from "${project.projectDir}/build/tmp/rdoc/doc"
}

distTar.dependsOn rdocJar
rdocJar.dependsOn runRDoc
runRDoc.dependsOn copyFilesForRDoc
copyFilesForRDoc.dependsOn cleanFilesForRDoc

distTar.dependsOn jarModels
jarModels.dependsOn compileModels
compileModels.dependsOn translateCpu
compileModels.dependsOn translateVliw
compileModels.dependsOn translateVmem
compileModels.dependsOn translateMinimips
compileModels.dependsOn translateX86Gnu
compileModels.dependsOn translateX86Nasm

task copyModelsJar(type: Copy) {
  from jarModels
  into "${project.projectDir}/build/target/lib/jars"
}

copyModelsJar.dependsOn jarModels

task generateMinimipsTemplates(type: JavaExec) {
  classpath = files("${project.projectDir}/build/libs/microtesk.jar")
  classpath += sourceSets.main.runtimeClasspath
  environment 'MICROTESK_HOME', "${project.projectDir}/build/target"
  main = mainClassName
  args = [
    '-gt',
    'minimips',
    '-btn',
    'MiniMipsBaseTemplate',
    '-btp',
    'minimips_base',
  ]
}

generateMinimipsTemplates.dependsOn copyModelsJar

distTar.dependsOn generateMinimipsTemplates

tasks.withType(Tar) {
  compression = Compression.GZIP
  extension = 'tar.gz'
}

distributions {
  main {
    baseName = projectName

    contents {
      rename '(.)*(?<!-src|microtesk|-javadoc|-rubydoc).jar', 'jars/$0'
      rename '(.)*microtesk.jar', 'lib/jars/microtesk.jar'

      into('lib') {
        from jarModels
      }

      // Copies all automatically generated templates
      from("${project.projectDir}/build/target/arch") {
        into('arch')
      }

      from('src/main/arch') {
        // EXCLUDED FROM RELEASE (NOT READY)
        exclude("**/*.py")
        into('arch')
      }

      from('build/generated-src/models') {
        into('gen')
      }

      from('src/main/bin') {
        into('bin')
      }

      from('src/main/ruby') {
        into('lib/ruby')
      }
/* // EXCLUDED FROM RELEASE (NOT READY)
      from('src/main/python') {
        into('lib/python')
      }
//*/
/**/ // EXCLUDED FROM RELEASE (NOT READY)
      from("${project.projectDir}/build/target/lib/why3") {
        into('lib/why3')
      }
//*/
      from(sourcesJar) {
        into('src')
      }

      from(javadocJar) {
        into('doc')
      }

      from(rdocJar) {
        into('doc')
      }

      from('src/main/etc') {
        into('etc')
      }

      from(fileTree('xdocs').files) {
        into('doc')
        include('**/*microtesk*.pdf')
      }

      from('ChangeLog')
      from('LICENSE')
      from('NOTICE')
      from('src/main/text/README.md')

      from('src/main/text/README_TOOLS.md') {
        into('tools')
        rename('README_TOOLS.md', 'README.md')
      }
    }
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      groupId projectGroup
      artifactId projectName
      version project.version
      from components.java

      artifact(sourcesJar) {
        classifier = "sources"
      }

      artifact(distTar)
    }
  }

  repositories {
    maven {
      if(project.version.endsWith('-SNAPSHOT')) {
        url repoUrlStr + 'snapshots'
      } else {
        url repoUrlStr + 'releases'
      }
      credentials {
        username repoUserStr
        password repoPasswordStr
      }
      authentication {
        basic(BasicAuthentication)
      }
    }
  }
}

afterReleaseBuild.dependsOn publish
